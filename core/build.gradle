plugins {
    id 'com.android.library'
    id 'com.squareup.leakcanary.deobfuscation'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}
apply from: '../shared_dependencies.gradle'

def properties = new Properties()
try {
    properties.load(new FileInputStream(rootProject.file("local.properties")))
} catch (Exception ignored) {
    logger.warn("No local.properties file such")
}

leakCanary {
    // LeakCanary needs to know which variants have obfuscation turned on
    filterObfuscatedVariants { variant ->
        variant.name == "debug"
    }
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 23
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // set CREDENTIAL KEYS in BuildConfig
        buildConfigField "String", "HOSTNAME", "\"${properties.getProperty("HOSTNAME")}\""
        buildConfigField "String", "API_KEY", "\"${properties.getProperty("API_KEY")}\""
        buildConfigField "String", "BASE_URL", "\"${properties.getProperty("BASE_URL")}\""
        buildConfigField "String", "IMAGE_URL", "\"${properties.getProperty("IMAGE_URL")}\""
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    api "io.reactivex.rxjava2:rxjava:$rxjava_version"
    api "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "androidx.room:room-rxjava2:$room_version"

    implementation "net.zetetic:android-database-sqlcipher:$android_database_sqlcipher_version"
    implementation "androidx.sqlite:sqlite-ktx:$sqlite_ktx_version"
}